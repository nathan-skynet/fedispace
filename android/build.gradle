buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
    project.configurations.all {
        resolutionStrategy {
            force 'androidx.browser:browser:1.8.0'
            force 'androidx.activity:activity:1.9.0'
            force 'androidx.activity:activity-ktx:1.9.0'
            force 'androidx.core:core:1.13.1'
            force 'androidx.core:core-ktx:1.13.1'
        }
    }
}

// Force consistent JVM target for receive_sharing_intent plugin
gradle.projectsEvaluated {
    def rsiProject = rootProject.subprojects.find { it.name == 'receive_sharing_intent' }
    if (rsiProject != null) {
        if (rsiProject.plugins.hasPlugin('com.android.library')) {
            rsiProject.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
        rsiProject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}
